#labels Featured,Phase-Design
L2 (aka OOC) 语言的定义

= 语法 =
{{{
(1) <prog> ::= <classdecl>{<classdecl>}
(2) <classdecl> ::= 'class' [ 'abstract' ] id [ ':' id ] '{' [<condecl>][ 'static' <memebr>]
[ 'private' <member>] [ 'public' <member>] [ 'redef' {<cfdef>}] '}'
(3) <condecl> ::= 'const' <condef>{ ',' <condef>} ';'
(4) <condef> ::= id '=' [ '-' ] num
(5) <member> ::= [<vdecl>]{<fdef>}
(6) <vdecl> ::= 'var' <decllist> 'end'
(7) <decllist> ::= <decl>{ ';' <decl>}
(8) <decl> ::= <type> <idlist>
(9) <type> ::= id {'[]'}
(10) <idlist> ::= id {',' id }
(11) <fdef> ::= <afdef> | <cfdef>
(12) <afdef> ::= 'func' 'abstract' <type> id '(' [<type> {','<type>}] ')'
(13) <cfdef> ::= 'func' <type> <head> '{' [<vdecl>]<stlist> '}'
(14) <head> ::=id '('[<paralist>]')'
(15) <stlist>::= <st>{';'<st>}
(16) <paralist> ::= <paradecl>{','<paradecl>}
(17) <st>::= <exp> | <cond> | <loop>
(18) <paradecl> ::= <type> id
(19) <cond> ::= 'if' '(' <exp> ')' <st>[ 'else' <st>]
(20) <loop> ::= 'while' '(' <exp> ')' [<st>]
(21) <exp> ::= <orexp>[ '=' <orexp>]
(22) <orexp> ::= <andexp>{ '||' <andexp>}
(23) <andexp> ::= <relexp>{ '&&' <relexp>}
(24) <relexp> ::= <term>[<relop> <term>]
(25) <relop> ::= '==' | '!=' | '<' | '>' | '<=' | '>='
(26) <term> ::= <factor>{<addop> <factor>}
(27) <addop> ::= '+' | '-'
(28) <factor> ::= <uniexp>{<multop> <uniexp>}
(29) <multop> ::= '*' | '/' | '%'
(30) <uniexp> ::= {<uniop>}<postexp>
(31) <uniop> ::= '-' | '!' | '++' | '--' | 'chk'
(32) <postexp> ::= <entity>{<postfix>}
(33) <postfix> ::= '++' | '--' | <apara> | <index> | <aselect> | <tcast>
(34) <apara> ::= '(' [<exp>{ ',' <exp>}] ')'
(35) <index> ::= '[' <exp> ']'
(36) <aselect> ::= '.' id
(37) <tcast> ::= ':' <type>
(38) <entity> ::= id | num | <cast> | <alloc>
(39) <cast> ::= '('<stlist>')'
(40) <alloc> ::= 'new' <type> [ '[' <exp> ']' ]
}}}

= 类型系统 =
L2 语言支持如下基本类型:
  * int 整形  默认值为0
  * void 不存在这个类型的对象
  * nulltype 可能值只有一个特殊的null

支持如下的组合类型:
  * array 数组 数组元素类型可以是任何合法的L1类型，但是各个元素类型必须一致
  * class 类 类似Java 的class. 所有类都是Object 类的子类. 也就是说L2语言也是1个单根的语言

= 类 =
L2 语言中的类的概念类似Java.不过要注意以下几点:
  * private 成员是私有的,其他修饰符的成员都认为是公共的
  * const 成员作为类的公共的整型成员
  * 支持单继承

= 操作符/运算符 =
请参考L1语言相关部分.
不过要补充2点:
  # 数组类型支持成员操作符'.',不过只支持 'length' 这个成员
  # 对于双目操作符要求的类型匹配定义如下:
    * 相同类型
    * 对于类类型, 同nulltype 和 子类匹配

= 流程控制 =
L2 支持条件和循环结构
具体语法见上面的语法定义



